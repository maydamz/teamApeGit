# Basic 3D movement script

# Player setup:
# CharacterBody3D
# |- MeshInstance3D
# |- CollisionShape3D
# |- Node3dD
#	 |- Camera3D

# Input mapping:
# move_forward: W (physical)
# move_left: A (physical)
# move_right: S (physical)
# move_backward: D (physical)

extends CharacterBody3D

# Movement variables
const SPEED = 5.0

# Camera variables
const SENSITIVITY = 0.001
const BOB_FREQ = 2.0
const BOB_AMP = 0.08
var t_bob = 0.0

# Accessing head and camera as variables
@onready var head: Node3D = $Head
@onready var camera: Camera3D = $Head/Camera3D

# Locks mouse to the window center
func _ready():
	Input.set_mouse_mode(Input.MOUSE_MODE_CAPTURED)

# First person camera controls
func _unhandled_input(event):
	if event is InputEventMouseMotion:
		head.rotate_y(-event.relative.x * SENSITIVITY)
		camera.rotate_x(-event.relative.y * SENSITIVITY)
		camera.rotation.x = clamp(camera.rotation.x, deg_to_rad(-40), deg_to_rad(60))
		
func _physics_process(delta: float) -> void:
	# Gravity
	if not is_on_floor():
		velocity += get_gravity() * delta

	# Directional movement controls
	var input_dir := Input.get_vector("move_left", "move_right", "move_forward", "move_backward")
	var direction := (head.transform.basis * Vector3(input_dir.x, 0, input_dir.y)).normalized()
	if direction:
		velocity.x = direction.x * SPEED
		velocity.z = direction.z * SPEED
	else:
		velocity.x = move_toward(velocity.x, 0, SPEED)
		velocity.z = move_toward(velocity.z, 0, SPEED)
	
	# Enable head bobbing
	t_bob += delta * velocity.length() * float(is_on_floor())
	camera.transform.origin = _headbob(t_bob)
	
	# Collision handling with other bodies
	move_and_slide()

# Head bobbing
func _headbob(time) -> Vector3:
	var pos = Vector3.ZERO
	pos.y = sin(time * BOB_FREQ) * BOB_AMP
	pos.x = cos(time * BOB_FREQ / 2) * BOB_AMP
	return pos
